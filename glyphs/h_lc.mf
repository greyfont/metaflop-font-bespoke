%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Bespoke v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("h",4,asc#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(0.9u#,0.6u#);

%%% overshoot %%%
y8r=y7r=mean-px +o; % overshoot up
top y8=mean +o; % overshoot up
top y7=mean +o; % overshoot up

%%% construction %%%
x8r=if (incx>.5): .5w else: x1r+incx*(x6r-x1r) fi;
x7r=if (incx>.5): .5w else: x6r-incx*(x6r-x1r) fi;
y1r=(mean-px)-incy*((mean-px)-px);
if (y1r<5/8mean): y1r:=4.5/8mean; fi     % start of upper curve
y2=y3=0;
x2=x4=x1=x2l+.5(cont*px);
x3=w-x1;
x3=x3l-.5(cont*px);
y4=h;
x1=leftstemloc;
x6=w-x1;
y6r=y1r;
x1r=x4r=x1+(cont*px/2);
x6r=x6-(cont*px/2);
ang:=angle(z8r-z1r);

%%% penpositions %%%
penpos1(whatever,if (superness=.5): ((ang-90)/2) else: 0 fi);
penpos2(whatever,0);
penpos3(whatever,180);
penpos4(whatever,180);
penpos6(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);

%%% draw %%%
if (cont>1.5): 
	x1l:=x1r-px; fi
penstroke z3e---super_ql(6e,7e)---super_qr(8e,1e);
penstroke z2e---z4e;

% penlabels(1,2,3,4,5,6,7,8);

endchar;
