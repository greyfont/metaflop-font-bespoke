% box dimension definition %
beginadjchar(63,4,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.1u#,space+0.3u#);

% overshoot %
x5-x6=1.2*cont*px+o;
y4-y3=1.2*cont*px+o;
top y11=h+o;
top y12=h+o;
y12r=y11r=h-px+o;

% construction %
x1=x2=x4=x3=0.5w;
x1l=x2l=x1-(cont*px/2);
.5(x5+x6)=0.5w;
y1=y4+0.15*h;
y2=y16r;
y3=0;
y5=y6=.5(y4+y3);
x9=x10=leftstemloc;
x9r=x10r=x9+(cont*px/2);
x13=x14=w-x9;
x13r=x14r=x13-(cont*px/2);
x12r=x15r=x9r+0.5*(x14r-x9r)+0.5*incx*(x14r-x9r);
x11r=x9r+0.5*(x14r-x9r)-0.5*incx*(x14r-x9r);
x16r=x1;
y15=y16=(.5h);
y15r=y16r=(.5h)+.5px;
y9r=y14r=y16r+0.5*(y11r-y16r)-0.5*incy*(y11r-y16r);
y10r=y13r=y16r+0.5*(y11r-y16r)+0.5*incy*(y11r-y16r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,90);
pos12(whatever,right,90);
pos13(whatever,up,180);
pos14(whatever,up,180);
pos15(whatever,right,-90);
pos16(whatever,right,-90);

% draw %
penstroke z1e--z2e;
fill super_qr(4,5)--super_ql(5,3)--super_qr(3,6)--super_ql(6,4)--cycle;
penstroke z16e--super_qr(15e,14e)--z14e--z13e&super_ql(13e,12e);
numeric t,u;
path a,b;
t:=ypart(((infinity,y16r+0.5*(y11r-y16r)+apert*0.5*(y11r-y16r))--(0,y16r+0.5*(y11r-y16r)+apert*0.5*(y11r-y16r))) intersectiontimes (super_ql(10l,11l)--z12l));
z99=point t of (super_ql(10l,11l)--z12l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(10l,11l)--z12l))+90));
x98=w;
a:=subpath (t,infinity) of (super_ql(10l,11l)--z12l);
u:=ypart((z99--z98) intersectiontimes (super_ql(10r,11r)--z12r));
b:=subpath (infinity,u) of (super_ql(10r,11r)--z12r);
fill a--z12l--b--cycle;

penlabels(range 1 thru 150);

endchar;