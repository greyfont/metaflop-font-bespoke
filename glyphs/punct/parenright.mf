% box dimension definition %
beginadjchar(41,3,cap#,des#);

% sidebearings %
numeric l,r;
normal_adjust_fit(space-0.5u#,space+0.25u#);

% overshoot %
top y3=top y6=1.1h+o;
y3r=y6r=1.1h-px+o;
bot y4=bot y5=-1.1d-o;
y4r=y5r=px-1.1d-o;

% construction %
x1=x2=w-leftstemloc;
x1r=x2r=x1-(cont*px/2);
x3r=x4r=0+incx*((cont*px/2)+x1r);
x5=x6=0;
y1r=y4r+0.5*(y3r-y4r)-0.5*incy*(y3r-y4r);
y2r=y4r+0.5*(y3r-y4r)+0.5*incy*(y3r-y4r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,-90);
pos4(whatever,right,-90);
pos5(whatever,right,-90);
pos6(whatever,right,-90);

% draw %
penstroke z1e--z2e;
numeric t,u;
path a,b;
t:=ypart(((infinity,y4l+.25*apert*.25(y3r-y4r))--(0,y4l+.25*apert*.25(y3r-y4r))) intersectiontimes (super_qlt(1l,4l,.95)--z5l));
z100=point t of (super_qlt(1l,4l,.95)--z5l);
z101=z100+whatever*(dir(angle(direction t of (super_qlt(1l,4l,.95)--z5l))+90));
y101=0;
a:=subpath (t,0) of (super_qlt(1l,4l,.95)--z5l);
u:=ypart((z100--z101) intersectiontimes (super_qlt(1r,4r,.95)--z5r));
b:=subpath (0,u) of (super_qlt(1r,4r,.95)--z5r);
fill a--z2r--b--cycle;
t:=ypart(((0,y3l-.25*apert*.25(y3r-y4r))--(infinity,y3l-.25*apert*.25(y3r-y4r))) intersectiontimes (super_qlt(2l,3l,.95)--z6l));
z99=point t of (super_qlt(2l,3l,.95)--z6l);	
z98=z99+whatever*(dir(angle(direction t of (super_qlt(2l,3l,.95)--z6l))+90));
y98=0;
a:=subpath (t,0) of (super_qlt(2l,3l,.95)--z6l);
u:=ypart((z99--z98) intersectiontimes (super_qlt(2r,3r,.95)--z6r));
b:=subpath (0,u) of (super_qlt(2r,3r,.95)--z6r);
fill a--z1r--b--cycle;

penlabels(range 1 thru 150);

endchar;
