% box dimension definition %
beginadjchar(36,4.75,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.25u#);

% overshoot %
top y13=h+o;
top y14=h+o;
y13l=y14l=h-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x10=x12=leftstemloc+.35px;
x1r=x2r=x1+(cont*px/2);
x10l=x12l=x10+(cont*px/2);
x5=x6=w-x1;
x15=x16=w-x1-.35px;
x18=0.525w;
x5r=x6r=x6-(cont*px/2);
x15l=x16l=x15-(cont*px/2);
x8=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
x13=x10l+0.5*(x16l-x10l)-0.5*incx*(x16l-x10l);
x17=x10l+0.3*(x16l-x10l)-0.7*incx*(x16l-x10l);
x7=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x14=x10l+0.5*(x16l-x10l)+0.5*incx*(x16l-x10l);
x3=x10l+0.7*(x6r-x1r)+0.3*incx*(x6r-x1r);
x20=x21=x22=x23=0.5w;
x20r=x21r=x22r=x23r=x20+(cont*px/2);
y18=0.5h;
y18l=0.5h+.5px;
y18r=0.5h-.5px;
y3=y18-0.05*(y5-y6)-.25px;
y3r=y3-.5px;
y17=y18+0.05*(y12-y10)+.35px;
y17l=y17+.5px;
y1=y6=y8r+0.52*(y18r-y8r)-0.48*incy*(y18r-y8r);
y2=y5=y8r+0.52*(y18r-y8r)+0.48*incy*(y18r-y8r);
y10=y16=y18l+0.5*(y13l-y18l)-0.5*incy*(y13l-y18l);
y12=y15=y18l+0.5*(y13l-y18l)+0.5*incy*(y13l-y18l);
y20=h;
y21=1.2h;
y22=0;
y23=-.2h;
path trial;
trial=z12{down}..z18..{down}z6;
pair dzS;
dzS=direction 1 of trial;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
penpos3(whatever,(angle dzS-90));
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos10(whatever,up,0);
pos12(whatever,up,0);
pos13(whatever,right,90);
pos14(whatever,right,90);
pos15(whatever,up,180);
pos16(whatever,up,180);
penpos17(whatever,(angle dzS-90));
pos20(whatever,up,0);
pos21(whatever,up,0);
pos22(whatever,up,0);
pos23(whatever,up,0);

% draw %
penstroke super_qrt(13e,12e,1.02)..z10e{down}..z17e{dzS}--z3e{dzS}..z5e{down}..super_qlt(6e,7e,1.02);
penstroke z20e--z21e;
penstroke z22e--z23e;
numeric t,u;
path a,b;
t:=ypart(((-infinity,y8r+0.5*(y18r-y8r)-0.75*apert*0.5*(y18r-y8r))--(infinity,y8r+0.5*(y18r-y8r)-0.75*apert*0.5*(y18r-y8r))) intersectiontimes (super_ql(1l,8l)--z7l));
z100=point t of (super_ql(1l,8l)--z7l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(1l,8l)--z7l))-90));
x101=w;
a:=subpath (t,infinity) of (super_ql(1l,8l)--z7l);
u:=ypart((z100--z101) intersectiontimes (super_ql(1r,8r)--z7r));
b:=subpath (infinity,u) of (super_ql(1r,8r)--z7r);
fill a--z7r--b--cycle;
t:=ypart(((infinity,y18l+0.5*(y13l-y18l)+0.9*apert*0.5*(y13l-y18l))--(0,y18l+0.5*(y13l-y18l)+0.9*apert*0.5*(y13l-y18l))) intersectiontimes (super_ql(15r,14r)--z13r));
z99=point t of (super_ql(15r,14r)--z13r);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(15r,14r)--z13r))+90));
x98=0;
a:=subpath (t,infinity) of (super_ql(15r,14r)--z13r);
u:=ypart((z99--z98) intersectiontimes (super_ql(15l,14l)--z13l));
b:=subpath (infinity,u) of (super_ql(15l,14l)--z13l);
fill a--z13l--b--cycle;

penlabels(range 1 thru 150);

endchar;