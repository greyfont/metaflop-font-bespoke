%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Travellion v1.6 – a MetaFlop-Font                       %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado     %%%
%%% www.metaflop.com                                        %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("3",4.6,cap#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(space+0.7u#,space+0.7u#);
eight:=.03h;

%%% overshoot %%%
y4r=y3r=y3-.5px; % overshoot up
y8r=y7r=px -o; % overshoot down
y3=.5h+eight; % overshoot up
y4=.5h+eight; % overshoot up
bot y8=0 -o; % overshoot down
bot y7=0 -o; % overshoot down

%%% construction %%%
x8r=if (incx>.5): x1r+.5(x6r-x1r) else: x1r+incx*(x6r-x1r) fi;
x7r=if (incx>.5): x6r-.5(x6r-x1r) else: x6r-incx*(x6r-x1r) fi;
y1r=if (incy>.5): .5h else: y8r-incy*(y8r-y3r) fi;
y2r=if (incy>.5): .5h else: y3r+incy*(y8r-y3r) fi;
x8r=x3r; x7r=x4r;
x1=leftstemloc-.5(cont*px)+.5px;
x6=w-leftstemloc;
x6=x5;
y2r=y5r;
y6r=y1r;
x2=x1;
x1r=x2r=x1+(px/2);
x6r=x5r=x6-(cont*px/2);
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos1(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos2(whatever,0);
penpos3(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);

%%% draw %%%
penstroke z3e---super_qr(4e,5e)---super_ql(6e,7e)---super_qr(8e,1e)---z2e;

%%% overshoot %%%
y12r=y11r=h-px +o; % overshoot up
y16r=y15r=y16+.5px; % overshoot down
top y11=h +o; % overshoot up
top y12=h +o; % overshoot up
y16=.5h+eight; % overshoot down
y15=.5h+eight; % overshoot down

%%% construction %%%
x16r=if (incx>.5): x9r+.5(x14r-x9r) else: x9r+incx*(x14r-x9r) fi;
x15r=if (incx>.5): x14r-.5(x14r-x9r) else: x14r-incx*(x14r-x9r) fi;
y9r=if (incy>.5): y16r-.5(y16r-y11r) else: y16r-incy*(y16r-y11r) fi;
y10r=if (incy>.5): y11r+.5(y16r-y11r) else: y11r+incy*(y16r-y11r) fi;
x16r=x11r; x15r=x12r;
x9=(leftstemloc+eight)-.5(cont*px)+.5px;
x14=w-(leftstemloc+eight);
x14=x13;
y10r=y13r;
y14r=y9r;
x10=x9;
x9r=x10r=x9+(px/2);
x14r=x13r=x14-(cont*px/2);
ang:=angle(z14r-z15r);

%%% penpositions %%%
penpos9(whatever,0);
penpos10(whatever,if (superness=.5): ((ang-90)/2) else: 0 fi);
penpos11(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos12(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos13(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos14(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos15(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos16(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);

%%% draw %%%
penstroke z9e---super_ql(10e,11e)---super_qr(12e,13e)---super_ql(14e,15e)---z16e;

% penlabels(range 1 thru 16);

endchar;
