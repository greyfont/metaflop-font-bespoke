% box dimension definition %
beginadjchar("3",4.25,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.1u#,space+0.4u#);

% overshoot %
bot y8=0-o;
bot y7=0-o;
y8r=y7r=px-o;
top y11=h+o;
top y12=h+o;
y12r=y11r=h-px+o;

% construction %
x1=x2=x9=x10=leftstemloc;
x1r=x2r=x9r=x10r=x1+(cont*px/2);
x5=x6=x13=x14=w-x1;
x5r=x6r=x13r=x14r=x6-(cont*px/2);
x4r=x7r=x12r=x15r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x8r=x11r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
x3r=x16r=x1r+0.5*(x6r-x1r)-0.35*incx*(x6r-x1r);
x17=0.4w;
y3=y4=y16=y17=(.5h);
y3r=y4r=y17r=(.5h)-.5px;
y15r=y16r=(.5h)+.5px;
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y9r=y14r=y16r+0.5*(y11r-y16r)-0.5*incy*(y11r-y16r);
y10r=y13r=y16r+0.5*(y11r-y16r)+0.5*incy*(y11r-y16r);

% branches %
y15l=y3l-taper*cont*px;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,90);
pos12(whatever,right,90);
pos13(whatever,up,180);
pos14(whatever,up,180);
pos15(whatever,right,-90);
pos16(whatever,right,-90);
pos17(whatever,right,90);

% draw % 
penstroke z17e--z3e--super_qr(4e,5e)--super_ql(6e,7e)--z8e;
penstroke super_qr(15e,14e)--z14e--z13e&super_ql(13e,12e);
numeric t,u;
path a,b;
t:=ypart(((-infinity,y8r+0.5*(y3r-y8r)-apert*0.5*(y3r-y8r))--(infinity,y8r+0.5*(y3r-y8r)-apert*0.5*(y3r-y8r))) intersectiontimes (super_ql(1l,8l)--z7l));
z100=point t of (super_ql(1l,8l)--z7l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(1l,8l)--z7l))-90));
x101=w;
a:=subpath (t,infinity) of (super_ql(1l,8l)--z7l);
u:=ypart((z100--z101) intersectiontimes (super_ql(1r,8r)--z7r));
b:=subpath (infinity,u) of (super_ql(1r,8r)--z7r);
fill a--z7r--b--cycle;
t:=ypart(((infinity,y15r+0.5*(y11r-y15r)+apert*0.5*(y11r-y15r))--(0,y15r+0.5*(y11r-y15r)+apert*0.5*(y11r-y15r))) intersectiontimes (super_ql(10l,11l)--z12l));
z99=point t of (super_ql(10l,11l)--z12l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(10l,11l)--z12l))+90));
x98=w;
a:=subpath (t,infinity) of (super_ql(10l,11l)--z12l);
u:=ypart((z99--z98) intersectiontimes (super_ql(10r,11r)--z12r));
b:=subpath (infinity,u) of (super_ql(10r,11r)--z12r);
fill a--z12l--b--cycle;

penlabels(range 1 thru 150);

endchar;