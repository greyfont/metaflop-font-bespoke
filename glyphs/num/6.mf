%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Travellion v1.6 – a MetaFlop-Font                       %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado     %%%
%%% www.metaflop.com                                        %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("6",4.6,cap#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(space*0.7u#,space*0.7u#);
eight:=.07h;
%%% overshoot %%%
y4r=y3r=y3-.5px; % overshoot up
y8r=y7r=px -o; % overshoot down
y3=.5h+eight; % overshoot up
y4=.5h+eight; % overshoot up
bot y8=0 -o; % overshoot down
bot y7=0 -o; % overshoot down

%%% construction %%%
x8r=if (incx>.5): .5w else: x1r+incx*(x6r-x1r) fi;
x7r=if (incx>.5): .5w else: x6r-incx*(x6r-x1r) fi;
y1r=if (incy>.5): .5h else: y8r-incy*(y8r-y3r) fi;
y2r=if (incy>.5): .5h else: y3r+incy*(y8r-y3r) fi;
x8r=x3r; x7r=x4r;
x1=leftstemloc;
x6=w-x1;
x6=x5;
y2r=y5r;
y6r=y1r;
x2=x1;
x1r=x2r=x1+(cont*px/2);
x6r=x5r=x6-(cont*px/2);
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos1(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos2(whatever,if (superness=.5): ((ang-90)/2) else: 0 fi);
penpos3(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);

%%% draw %%%
penstroke super_qr(4e,5e)---super_ql(6e,7e)---super_qr(8e,1e)---super_ql(2e,3e)---cycle;

%%% overshoot %%%
y12r=y11r=h-px +o; % overshoot up
y16r=y15r=y16+.5px; % overshoot down
top y11=h +o; % overshoot up
top y12=h +o; % overshoot up
y16=.5h+eight; % overshoot down
y15=.5h+eight; % overshoot down

%%% construction %%%
x16r=if (incx>.5): x9r+.5(x14r-x9r) else: x9r+incx*(x14r-x9r) fi;
x15r=if (incx>.5): x14r-.5(x14r-x9r) else: x14r-incx*(x14r-x9r) fi;
y14r=if (incy>.5): y16r-.5(y16r-y11r) else: y16r-incy*(y16r-y11r) fi;
y13r=if (incy>.5): y11r+.5(y16r-y11r) else: y11r+incy*(y16r-y11r) fi;
x16r=x11r; x15r=x12r;
x9=leftstemloc;
x14=(w-(x9+.04h))+.5(cont*px)-.5px;
x14=x13;
if (cont>1.2): y10r=if (incx>.5): y3l; else: y13r; fi else: y10r=y3l; fi
if (cont>1.2): y9r=if (incx>.5): y3l; else: y14r; fi else: y9r=y3l; fi
x10=x9;
x9r=x10r=x9+(cont*px/2);
x14r=x13r=x14-(px/2);
ang:=angle(z14r-z15r);

%%% penpositions %%%
penpos9(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos10(whatever,if (superness=.5): ((angle(z11r-z10r)-90)/2) else: 0 fi);
penpos11(whatever,if (superness=.5): -(180-angle(z11r-z10r))/2 else: -90 fi);
penpos12(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos13(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos14(whatever,180);
penpos15(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos16(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);

%%% draw %%%
penstroke z2e---super_ql(10e,11e)---super_qr(12e,13e)---z14e;



% penlabels(range 1 thru 16);

endchar;
