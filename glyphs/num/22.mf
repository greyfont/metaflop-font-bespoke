% box dimension definition %
beginadjchar("2",4.25,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.3u#);

% overshoot %
top y11=h+o;
top y12=h+o;
y12r=y11r=h-px+o;

% construction %
x1=x2=x9=x10=leftstemloc;
x1r=x2r=x9r=x10r=x1+(cont*px/2);
x13=x14=w-x1;
x8=w-x1+.5px;
x13r=x14r=w-x1r;
x4l=x12r=x15r=x2r+0.5*(x14r-x2r)+0.5*incx*(x14r-x2r);
x3r=x11r=x16l=x2r+0.5*(x14r-x2r)-0.5*incx*(x14r-x2r);
x7r=x1l; 
y1=0;
bot y7=0;
bot y8=0;
y2r=y8r+0.3*(y4r-y8r)+0.7*incy*(y4r-y8r);
y7r=y8r=y7+px/2;
y3=y4=y15=y16=(.5h);
y3r=y4r=(.5h)-.5px;
y15r=y16r=(.5h)+.5px;
y10r=y13r=y16r+0.5*(y11r-y16r)+0.5*incy*(y11r-y16r);
y9r=y14r=y16r+0.5*(y12r-y16r)-0.5*incy*(y12r-y16r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos7(whatever,right,90);
pos8(whatever,right,90);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,90);
pos12(whatever,right,90);
pos13(whatever,up,180);
pos14(whatever,up,180);
pos15(whatever,right,-90);
pos16(whatever,right,-90);

% draw %
penstroke z8e--z7e;
penstroke z1e--super_ql(2e,3e)--z4e;
penstroke super_qr(15e,14e)--z14e--z13e&super_ql(13e,12e);
numeric t,u;
path a,b;
t:=ypart(((infinity,y16r+0.5*(y11r-y16r)+apert*0.5*(y11r-y16r))--(0,y16r+0.5*(y11r-y16r)+apert*0.5*(y11r-y16r))) intersectiontimes (super_ql(10l,11l)--z12l));
z99=point t of (super_ql(10l,11l)--z12l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(10l,11l)--z12l))+90));
x98=w;
a:=subpath (t,infinity) of (super_ql(10l,11l)--z12l);
u:=ypart((z99--z98) intersectiontimes (super_ql(10r,11r)--z12r));
b:=subpath (infinity,u) of (super_ql(10r,11r)--z12r);
fill a--z12l--b--cycle;

penlabels(range 1 thru 150);

endchar;