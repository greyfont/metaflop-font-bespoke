%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Bespoke v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("M",6,cap#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(1.1u#,1.1u#);

%%% construction %%%
x15l=x16l=leftstemloc-cont*px/2;
x13r=x14r=w-x15l;
x15r=x16r=x15+(px/2);
x13l=x14l=x14r-cont*px;
y15=y13=0;
y16=y14=h;
x3l=x16r-px;
(x4l+x1r)/2=(x16r+x13l)/2;
x2r=x14l+px;
x1r-x4l=corner;
y1=y4=0;
y2=y3=h;

%%% penpositions %%%
penpos15(whatever,0);
penpos16(whatever,0);
penpos13(whatever,0);
penpos14(whatever,0);
alpham=diag_ratio(1,cont*px,h,x3l-x4l);
penpos1(alpham*px,0);
penpos2(alpham*px,0);
penpos3(alpham*cont*px,0);
penpos4(alpham*cont*px,0);

%%% draw %%%
penstroke z15e..z16e;
penstroke z13e..z14e;
z0=whatever[z3r,z4r]=whatever[z1l,z2l];
fill z3l--z4l--z1r--z2r--z2l--z0--z3r--cycle;

% penlabels(range 0 thru 30);

endchar;
