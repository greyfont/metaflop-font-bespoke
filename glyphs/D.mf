%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Transit v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("D",corr*5.25,cap#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(1.1u#,0.7u#);

%%% construction %%%
x11=x22=leftstemloc;
x11r=x22r=x11+(cont*px/2);
x7r=x6r-incx*(x6r-x11r);   % setting width
y5r=if (incy>.5): .5h else: y4r+incy*(y7r-y4r) fi;
y6r=if (incy>.5): .5h else: y7r-incy*(y7r-y4r) fi;
x7r=x4r;
y4r=y2r=h-px;
y7r=y1r=px;
top y4=h;
y4=y2;
bot y7=0;
y7=y1;
y11=0;
y22=h;
x1=x11;
x6=w-x11;
x6=x5;
x2=x1;
x6r=x5r=x6-(cont*px/2);
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos1(whatever,-90);
penpos2(whatever,90);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos11(whatever,0);
penpos22(whatever,0);

%%% draw %%%
penstroke z2e---super_qr(4e,5e)---super_ql(6e,7e)--z1e;
penstroke z11e---z22e;

% penlabels(range 1 thru 30);

endchar;
