% box dimension definition %
beginadjchar(12,2.5,cap#,des#);

% sidebearings %
numeric l,r;
normal_adjust_fit(space-0.25u#,space-0.25u#);

% overshoot %
bot y8r=-d;
bot y7r=-d;
y8=y7=y8r-(px/2.5);

% construction %
x5=x6=px/2;
x5r=x6r=x5+(cont*px/2.5);
x1=x2=w-x5;
x1r=x2r=w-x5r;
x3r=x7r=x6r+0.5*(x2r-x6r)+0.5*incx*(x2r-x6r);
x8r=x6r+0.5*(x2r-x6r)-0.5*incx*(x2r-x6r);
x13r=.675w;
x14r=.4w;
y1=mean;
y2r=y6r=y7r-0.5*(y7r-y3r)+0.5*incy*(y7r-y3r);
y5=y7r-0.5*(y7r-y3r)-0.5*incy*(y7r-y3r);
y3=-.3d;
y3r=y3-px/2.5;
y13=px/2.5;
y13r=y13-px/2.5;
y14=y14r+px/2.5;
y14r=-.3d-px/5;
path trial;
trial=z5{up}..z14r--z13r;
pair dz;
dz=direction 1 of trial;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,-90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos13(whatever,right,(angle -dz)-90);
pos14(whatever,right,(angle -dz)-90);

% draw %
penstroke z13e{-dz}--z14e{z5-z13}..z5e{down}--z6e & super_ql(6e,8e)--z7e;
numeric t,u;
path a,b;
t:=ypart(((-infinity,-0.5d-1.4apert*0.5d)--(infinity,-0.5d-1.4apert*0.5d)) intersectiontimes (super_ql(2l,7l)--z8l));
z100=point t of (super_ql(2l,7l)--z8l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(2l,7l)--z8l))+90));
x101=0;
a:=subpath (t,infinity) of (super_ql(2l,7l)--z8l);
u:=ypart((z100--z101) intersectiontimes (super_ql(2r,7r)--z8r));
b:=subpath (infinity,u) of (super_ql(2r,7r)--z8r);
fill a--z8r--b--cycle;

% composite anchor %
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;