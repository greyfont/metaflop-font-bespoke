% box dimension definition %
beginadjchar("m",6.5,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.4u#,space+0.35u#);

% overshoot %
top y3=mean+o;
top y4=mean+o;
top y9=mean+o;
top y10=mean+o;
y3r=y4r=y9r=y10r=mean-px+o;

% construction %
x1=x13=leftstemloc;
x1r=x2r=x13r=x1+(cont*px/2);
x5=x6=x8=0.5w;
x5r=x6r=x6-(cont*px/2);
x7r=x5l;
x8r=x6l;
x11=x12=w-x1;
x11r=x12r=x12-(cont*px/2);
x3r=x1r+0.6(x6r-x1r)-0.5*incx*(x6r-x1r);
x4r=x1r+0.6(x6r-x1r)+0.5*incx*(x6r-x1r);
x9r=x6l+0.6(x12r-x8r)-0.5*incx*(x12r-x8r);
x10r=x6l+0.6(x12r-x8r)+0.5*incx*(x12r-x8r);
y1=y6=y8=y12=0;
y13=mean;
y2r=y5r=y7r=y11r=0.7*(y3r-y1r)+0.3*incy*(y3r-y1r);

% branches %
x2l=x2r-taper*cont*px;
x7l=x7r-taper*cont*px;

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,0);
penpos8(whatever,0);
penpos9(whatever,90);
penpos10(whatever,90);
penpos11(whatever,180);
penpos12(whatever,180);
penpos13(whatever,0);

% draw %
penstroke super_ql(2e,3e)---super_qr(4e,5e)---z6e;
penstroke super_ql(7e,9e)---super_qr(10e,11e)---z12e;
penstroke z1e---z13e;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.35h+px/2);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;