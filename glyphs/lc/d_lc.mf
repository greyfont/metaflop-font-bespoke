%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Bespoke v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("d",4.4, asc#, 0);

%%% sidebearings %%%

if (sidebearing=1):

numeric l,r;
normal_adjust_fit(space*0.5u#,space*0.9u#);

fi

%%% overshoot %%%
y4r=y3r=mean-px +o;    % +o overshoot
y8r=y7r=px -o;       % -o overshoot
top y3=mean +o;    % +o overshoot
top y4=mean +o;    % +o overshoot
bot y8=0-o;     % -o overshoot
bot y7=0-o;     % -o overshoot

%%% construction %%%
x11=x22=w-leftstemloc;
x11r=x22r=x11+(cont*px/2);
y11=0;
y22=h;
x8r=if (incx>.5): .5w else: x1r+incx*(x6r-x1r) fi;
x7r=if (incx>.5): .5w else: x6r-incx*(x6r-x1r) fi;
x8r=x3r; x7r=x4r;
x1=leftstemloc;
x6=w-x1;
x6=x5;
y2r=y5r;
y6r=y1r;
x2=x1;
x1r=x2r=x1+(cont*px/2);
x6r=x5r=x6-(cont*px/2);
y1r=if (incy>.5): .5mean else: y8r-incy*(y8r-y3r) fi;
y2r=if (incy>.5): .5mean else: y3r+incy*(y8r-y3r) fi;
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos11(whatever,0);
penpos22(whatever,0);
penpos1(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos2(whatever,if (superness=.5): ((ang-90)/2) else: 0 fi);
penpos3(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);

%%% draw %%%
if (cont>1.5): 
	x5l:=x5r+px; x6l:=x5l; fi
penstroke super_qr(4e,5e)---super_ql(6e,7e)---super_qr(8e,1e)---super_ql(2e,3e)---cycle;
penstroke z11e..z22e;

% penlabels(1,2,3,4,5,6,7,8);

endchar;
