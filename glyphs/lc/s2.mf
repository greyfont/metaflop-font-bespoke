% box dimension definition %
beginadjchar("s",3.8,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.1u#,space+0.1u#);

% overshoot %
top y13=h+o;
top y14=h+o;
y13r=y14r=h-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=x10=x12=leftstemloc;
x1r=x2r=x10r=x12r=x1+(cont*px/2);
x5=x6=x15=x16=w-x1;
x5r=x6r=x15r=x16r=x6-(cont*px/2);
x8r=x10r+0.5*(x16r-x10r)-0.5*incx*(x16r-x10r);
x3r=x13r=x18l=x10r+0.5*(x16r-x10r)-0.5*incx*(x16r-x10r);
x4r=x7r=x14r=x17l=x10r+0.5*(x16r-x10r)+0.5*incx*(x16r-x10r);
y3=y4=y17=y18=(.5h);
y4r=y3r=(.5h)-.5px;
y17r=y18r=(.5h)+.5px;
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y10r=y16r=y18r+0.5*(y13r-y18r)-0.5*incy*(y13r-y18r);
y12r=y15r=y18r+0.5*(y13r-y18r)+0.5*incy*(y13r-y18r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos10(whatever,up,0);
pos12(whatever,up,0);
pos13(whatever,right,90);
pos14(whatever,right,90);
pos15(whatever,up,180);
pos16(whatever,up,180);
pos17(whatever,right,90);
pos18(whatever,right,90);

% draw %
penstroke z3e--super_qr(4e,5e)--super_ql(6e,7e)--z8e;
penstroke super_qr(18e,10e)--z10e--z12e&super_ql(12e,13e);
numeric t,u;
path a,b;
t:=ypart(((-infinity,y8l+0.5*(y18r-y8r)-0.75*apert*0.5*(y18r-y8r))--(infinity,y8l+0.5*(y18r-y8r)-0.75*apert*0.5*(y18r-y8r))) intersectiontimes (super_ql(1l,8l)--z7l));
z100=point t of (super_ql(1l,8l)--z7l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(1l,8l)--z7l))-90));
x101=w;
a:=subpath (t,infinity) of (super_ql(1l,8l)--z7l);
u:=ypart((z100--z101) intersectiontimes (super_ql(1r,8r)--z7r));
b:=subpath (infinity,u) of (super_ql(1r,8r)--z7r);
fill a--z7r--b--cycle;
t:=ypart(((infinity,y18l+0.5*(y13l-y18l)+0.75*apert*0.5*(y13l-y18l))--(0,y18l+0.5*(y13l-y18l)+0.75*apert*0.5*(y13l-y18l))) intersectiontimes (super_ql(15l,14l)--z13l));
z99=point t of (super_ql(15l,14l)--z13l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(15l,14l)--z13l))+90));
x98=0;
a:=subpath (t,infinity) of (super_ql(15l,14l)--z13l);
u:=ypart((z99--z98) intersectiontimes (super_ql(15r,14r)--z13r));
b:=subpath (infinity,u) of (super_ql(15r,14r)--z13r);
fill a--z13l--b--cycle;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.525h);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;