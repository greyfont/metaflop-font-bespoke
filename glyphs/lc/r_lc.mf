%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                         %
% MF Bespoke v1.8                                                         %
% a Metaflop-font © 2012 by Simon Egli, Marco Müller                      %
% www.metaflop.com                                                        %
% licensed under gpl v3: www.gnu.org/licenses/                            %
%                                                                         %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% box dimension definition %
beginadjchar("r",3.5,mean#,0);

% sidebearings %
if (sidebearing=1):
 numeric l,r;
 normal_adjust_fit(space+0.4u#,space-0.1u#); fi

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;

% construction %
x1=x2=x10=leftstemloc;
x1r=x2r=x10r=x1+(cont*px/2);
x5=x6=w-x2;
x5r=x6r=x6-(cont*px/2);
x4r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1=y6=0;
y10=mean;
y2r=y5r=0.7*(y3r-y1r)+0.3*incy*(y3r-y1r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos10(whatever,0);

% draw %
penstroke z1e..z10e;
penstroke super_ql(2e,3e);

z99=((0,0.5mean+appert*0.5mean)--(infinity,0.5mean+appert*0.5mean)) intersectionpoint (z3l---super_qr(4l,5l)---z6l);
path x; numeric t,u; (t,u)=((0,0.5mean+appert*0.5mean)--(infinity,0.5mean+appert*0.5mean)) intersectiontimes (z3l---super_qr(4l,5l)---z6l);
	
z98=z99+whatever*(dir(angle(direction u of (z3l---super_qr(4l,5l)---z6l))+90));
x98=0;
path b; numeric t,u; (t,u)=((z99--z98) intersectiontimes (z3r---super_qr(4r,5r)---z6r));
b = subpath (0,u) of (z3r---super_qr(4r,5r)---z6r);
path a; numeric t,u; (t,u)=(((0,0.5mean+appert*0.5mean)---(infinity,0.5mean+appert*0.5mean)) intersectiontimes (z3l---super_qr(4l,5l)---z6l));
a = subpath (u,0) of (z3l---super_qr(4l,5l)---z6l);
fill a---z3l---z3r---b---cycle;

% grayfont proof mode
% penlabels(range 1 thru 150);

endchar;
