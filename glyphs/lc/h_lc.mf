%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                         %
% Bespoke v1.7                                                            %
% a Metaflop-font © 2012 by Simon Egli, Marco Müller                      %
% thanks to Teofilo Furtado                                               %
% www.metaflop.com                                                        %
%                                                                         %
% This program is free software: you can redistribute it and/or modify    %
% it under the terms of the GNU General Public License as published by    %
% the Free Software Foundation, either version 3 of the License, or       %
% (at your option) any later version.                                     %
%                                                                         %
% This program is distributed in the hope that it will be useful,         %
% but WITHOUT ANY WARRANTY; without even the implied warranty of          % 
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            % 
% GNU General Public License for more details.                            %
%                                                                         %
% You should have received a copy of the GNU General Public License       %
% along with this program. If not, see <http://www.gnu.org/licenses/>.    %
%                                                                         %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% box dimension definition %
beginadjchar("h",4,asc#,0);

% sidebearings %
if (sidebearing=1):
numeric l,r;
normal_adjust_fit(space+0.5u#,space+0.35u#); fi

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;

% construction %
x1=x2=x10=leftstemloc;
x1r=x2r=x10r=x1+(cont*px/2);
x5=x6=w-x2;
x5r=x6r=x6-(cont*px/2);
x4r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1=y6=0;
y10=h;
y2r=y5r=(0.5*mean)+(0.5*incy*mean)+(px/2);
ang:=angle(z3r-z2r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,if (superness=.5): (ang-90)/2 else: 0 fi);
penpos3(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,180);
penpos10(whatever,0);

% draw %
penstroke super_ql(2e,3e)---super_qr(4e,5e)---z6e;
penstroke z1e---z10e;

% penlabels(range 1 thru 150);

endchar;
