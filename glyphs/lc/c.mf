% box dimension definition %
beginadjchar("c",4.6,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.1u#-0.2*apert*u#);

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=w-x1;
x5r=x6r=x6-(cont*px/2);
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);

% draw %
penstroke super_qr(8e,1e)--super_ql(2e,3e);
numeric t,u;
path a,b;
t:=ypart(((-infinity,0.5h-0.9*apert*0.5(y3r-y8r))--(infinity,0.5h-0.9*apert*0.5(y3r-y8r))) intersectiontimes (super_ql(6l,7l)--z8l));
z100=point t of (super_ql(6l,7l)--z8l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(6l,7l)--z8l))+90));
x101=0;
a:=subpath (t,infinity) of (super_ql(6l,7l)--z8l);
u:=ypart((z100--z101) intersectiontimes (super_ql(6r,7r)--z8r));
b:=subpath (infinity,u) of (super_ql(6r,7r)--z8r);
fill a--z8r--b--cycle;
t:=ypart(((infinity,0.5h+0.9*apert*0.5(y3r-y8r))--(0,0.5h+0.9*apert*0.5(y3r-y8r))) intersectiontimes (super_ql(5l,4l)--z3l));
z99=point t of (super_ql(5l,4l)--z3l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(5l,4l)--z3l))+90));
x98=0;
a:=subpath (t,infinity) of (super_ql(5l,4l)--z3l);
u:=ypart((z99--z98) intersectiontimes (super_ql(5r,4r)--z3r));
b:=subpath (infinity,u) of (super_ql(5r,4r)--z3r);
fill a--z3l--b--cycle;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.525h);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;