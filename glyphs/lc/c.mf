% box dimension definition %
beginadjchar("c",4.6,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.25u#,space+0.25u#-0.2*appert*u#);

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=w-x1;
x5r=x6r=x6-(cont*px/2);
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos8(whatever,-90);

% draw %
penstroke super_qr(8e,1e)--super_ql(2e,3e);
z100=((0,0.5mean-0.9*appert*0.5(y3r-y8r))--(infinity,0.5mean-0.9*appert*0.5(y3r-y8r))) intersectionpoint (z5l--super_ql(6l,7l)--z8l);
path x; numeric t,u; (t,u)=((0,0.5mean-0.9*appert*0.5(y3r-y8r))--(infinity,0.5mean-0.9*appert*0.5(y3r-y8r))) intersectiontimes (z5l--super_ql(6l,7l)--z8l);
z101=z100+whatever*(dir(angle(direction u of (z5l--super_ql(6l,7l)--z8l))+90));
x101=0;
path r; numeric t,u; (t,u)=((z100--z101) intersectiontimes (z5r--super_ql(6r,7r)--z8r));
r = subpath (u,infinity) of (z5r--super_ql(6r,7r)--z8r);
path ss; numeric t,u; (t,u)=(((0,0.5mean-0.9*appert*0.5(y3r-y8r))--(infinity,0.5mean-0.9*appert*0.5(y3r-y8r))) intersectiontimes (z5l--super_ql(6l,7l)--z8l));
ss = subpath (infinity,u) of (z5l--super_ql(6l,7l)--z8l);
fill z8r--z8l--ss--r--cycle;

z99=((0,0.5mean+0.9*appert*0.5(y3r-y8r))--(infinity,0.5mean+0.9*appert*0.5(y3r-y8r))) intersectionpoint (z3l--super_qr(4l,5l)--z6l);
path x; numeric t,u; (t,u)=((infinity,0.5mean+0.9*appert*0.5(y3r-y8r))--(0,0.5mean+0.9*appert*0.5(y3r-y8r))) intersectiontimes (z3l--super_qr(4l,5l)--z6l);	
z98=z99+whatever*(dir(angle(direction u of (z3l--super_qr(4l,5l)--z6l))+90));
x98=0;
path b; numeric t,u; (t,u)=((z99--z98) intersectiontimes (z3r--super_qr(4r,5r)--z6r));
b = subpath (0,u) of (z3r--super_qr(4r,5r)--z6r);
path a; numeric t,u; (t,u)=(((0,0.5mean+0.9*appert*0.5(y3r-y8r))--(infinity,0.5mean+0.9*appert*0.5(y3r-y8r))) intersectiontimes (z3l--super_qr(4l,5l)--z6l));
a = subpath (u,0) of (z3l--super_qr(4l,5l)--z6l);
fill z3l--z3r--b--a--cycle;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.35h+px/2);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;