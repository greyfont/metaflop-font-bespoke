%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                         %
% Bespoke v1.7                                                            %
% a Metaflop-font © 2012 by Simon Egli, Marco Müller                      %
% thanks to Teofilo Furtado                                               %
% www.metaflop.com                                                        %
%                                                                         %
% This program is free software: you can redistribute it and/or modify    %
% it under the terms of the GNU General Public License as published by    %
% the Free Software Foundation, either version 3 of the License, or       %
% (at your option) any later version.                                     %
%                                                                         %
% This program is distributed in the hope that it will be useful,         %
% but WITHOUT ANY WARRANTY; without even the implied warranty of          % 
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            % 
% GNU General Public License for more details.                            %
%                                                                         %
% You should have received a copy of the GNU General Public License       %
% along with this program. If not, see <http://www.gnu.org/licenses/>.    %
%                                                                         %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% box dimension definition %
beginadjchar("e",4.4,mean#,0);

% sidebearings %
if (sidebearing=1):
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.2u#);
fi

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=x9=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=x11=w-x1;
x5r=x6r=x11r=x6-(cont*px/2);
x10=x5l;
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y9=y10=y11=0.5w;
y9l=y10l=y9-px/2;
ang:=angle(z6r-z7r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos8(whatever,-90);
penpos9(whatever,90);
penpos10(whatever,90);
penpos11(whatever,180);

% draw %
penstroke z9e..z10e;
penstroke super_qr(8e,1e)---super_ql(2e,3e)---super_qr(4e,5e);
penstroke z5e..z11e;
z100=((0,0.5mean-appert*0.5mean)--(infinity,0.5mean-appert*0.5mean)) intersectionpoint (z5l---super_ql(6l,7l)--z8l);
path x; numeric t,u; (t,u)=((0,0.5mean-appert*0.5mean)--(infinity,0.5mean-appert*0.5mean)) intersectiontimes (z5l---super_ql(6l,7l)--z8l);
z101=z100+whatever*(dir(angle(direction u of (z5l---super_ql(6l,7l)--z8l))+90));
x101=0;
path r; numeric t,u; (t,u)=((z100--z101) intersectiontimes (z5r---super_ql(6r,7r)--z8r));
r = subpath (u,infinity) of (z5r---super_ql(6r,7r)--z8r);
path ss; numeric t,u; (t,u)=(((0,0.5mean-appert*0.5mean)--(infinity,0.5mean-appert*0.5mean)) intersectiontimes (z5l---super_ql(6l,7l)--z8l));
ss = subpath (infinity,u) of (z5l---super_ql(6l,7l)--z8l);
fill z8r---z8l---ss---r---cycle;

% penlabels(range 1 thru 150);
endchar;