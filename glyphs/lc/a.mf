% box dimension definition %
beginadjchar("a",4.1,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.4u#);

% overshoot %
bot y8=0-o;
bot y7=0-o;
y8r=y7r=px-o;
top y11=h+o;
top y12=h+o;
y12r=y11r=h-px+o;

% construction %
x1=x2=x9=x10=leftstemloc;
x1r=x2r=x9r=x10r=x1+(cont*px/2);
x13=x14=w-x1;
x5r=x6r=x13r=x14r=x13-(cont*px/2);
x4r=x7r=x12r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x11r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y3=y4=0.6*mean-0.35px;
y3r=y4r=y3-px/2;
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y9r=y14r=0;
y10r=y13r=y3l+0.5*incy*(y11r-y3r);

% branches %
x5l=x6l=x5r+taper*cont*px;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,-90);
pos12(whatever,right,-90);
pos13(whatever,up,180);
pos14(whatever,up,180);

% draw %
penstroke super_qr(4e,5e)--super_ql(6e,7e)--super_qr(8e,1e)--super_ql(2e,3e)--cycle;
penstroke z14e--z13e&super_ql(13e,12e);
numeric t,u;
path a,b;
t:=ypart(((-infinity,y3l+1.2apert*(y11r-y3l))--(infinity,y3l+1.2apert*(y11r-y3l))) intersectiontimes (super_ql(10l,11l)--z12l));
z99=point t of (super_ql(10l,11l)--z12l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(10l,11l)--z12l))+90));
x98=w;
a:=subpath (t,infinity) of (super_ql(10l,11l)--z12l);
u:=ypart((z99--z98) intersectiontimes (super_ql(10r,11r)--z12r));
b:=subpath (infinity,u) of (super_ql(10r,11r)--z12r);
fill a--z12l--b--cycle;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.525h);
charanchorbots_[charcode]=(.85w,0);

penlabels(range 1 thru 150);

endchar;