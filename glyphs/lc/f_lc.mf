%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Bespoke v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("f",3.25,asc#,0);

%%% sidebearings %%%
if (sidebearing=1):
numeric l,r;
normal_adjust_fit(space+0.1u#,space+0.15u#);
fi

%%% overshoot %%%
top y12=h +o; % overshoot up
top y11=h +o; % overshoot up
y12r=y11r=h-px +o;  % overshoot up

%%% construction %%%
incf:=incx;
x9r= if (incf>.5): x12r+((w-(leftstemloc-.5(px*cont)+.5px))-x12r) else: w-(leftstemloc-.5(cont*px)+px) fi;
x12r=if (incf>.5):x14r+incf*(w-(leftstemloc-.5(cont*px)+px)-x14r)else: x14r+.5*(x9r-x14r) fi;
if (h>w):
	y10r=y12r-(incy*((w-leftstemloc+.5(cont*px)-px)-(.5w))) 
else: 
	y10r=y12r+incy*(px-y12r) fi;
x14=(.5w);
x14=x13;
y10r=y13r;
x10=x9;
x9r=x10r=x9-(px/2);
y14r=y9r=0;
x14r=x13r=x14+(cont*px/2);
x11r=x12r;
x15=1.5*leftstemloc-.5(cont*px); % width of bar (affecting sidebearings)
x16=w-x15;
y15r=y16r=mean;
ang:=(angle(z12r-z13r));

%%% penpositions %%%
penpos9(whatever,0);
penpos10(whatever,if (superness=.5): -((ang-90)/2) else: 0 fi);
penpos11(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos12(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos13(whatever,if (superness=.5): -(angle(z13r-z12r)-180)/2 else: 180 fi);
penpos14(whatever,180);
penpos15(px,90);
penpos16(px,90);
penstroke z15e---z16e;

%%% draw %%%
if (superness=.5): 
	penstroke z11e---super_qr(12e,13e)---z14e; else: penstroke super_qr(12e,13e)---z14e; fi
if (incf>.5):
	path tl; tl = z9l---super_ql(10l,11l);
	path ap; ap = (w-(leftstemloc-.5(cont*px)),infinity)---(w-(leftstemloc-.5(cont*px)),-infinity);
	path v; v = z9r---super_ql(10r,11r);
	z100=ap intersectionpoint tl;
	numeric t,u; (t,u)=ap intersectiontimes tl;
	z101=z100+whatever*(dir(angle(direction u of tl)+90));
	y101=0;
	path m; numeric t,u; (t,u)=((z100--z101) intersectiontimes (v));
	m = subpath (u,infinity) of (v);
	path ss; numeric t,u; (t,u)=(ap intersectiontimes tl);
	ss = subpath (infinity,u) of tl;
	fill ss---m---cycle;
else:
	path tl; tl = z9l---super_ql(10l,11l);
	path ap; ap = (0,y15r-appert*2(y15r-y12r))---(w,y15r-appert*2(y15r-y12r));
	path v; v = z9r---super_ql(10r,11r);
	z100=ap intersectionpoint tl;
	path x; numeric t,u; (t,u)=ap intersectiontimes tl;
	z101=z100+whatever*(dir(angle(direction u of tl)+90));
	x101=0;
	path m; numeric t,u; (t,u)=((z100--z101) intersectiontimes v);
	m = subpath (u,infinity) of v;
	path ss; numeric t,u; (t,u)=(ap intersectiontimes tl);
	ss = subpath (infinity,u) of tl;
	fill ss---m---cycle;fi

% penlabels(range 1 thru 80);

endchar;
