% box dimension definition %
beginadjchar(230,7,mean#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.25u#,space+0.15u#);

% overshoot %
top y11=h+o;
top y12=h+o;
top y23=mean+o;
top y24=mean+o;
bot y8=0-o;
bot y7=0-o;
bot y27=0-o;
bot y28=0-o;
y8r=y7r=y27r=y28r=px-o;
y12r=y11r=y23r=y24r=h-px+o;

% construction %
x1=x2=x9=x10=leftstemloc;
x1r=x2r=x9r=x10r=x1+(cont*px/2);
x6=x13=x14=x21=x22=x29=.475w;
x5r=x6r=x13r=x14r=x13-(cont*px/2);
x4r=x7r=x12r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x11r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
x21r=x22r=x21+(cont*px/2);
x25=x26=x31=w-x1;
x25r=x26r=x31r=x26-(cont*px/2);
x30=x25l;
x24r=x27r=x21r+0.5*(x26r-x21r)+0.5*incx*(x26r-x21r);
x23r=x28r=x21r+0.5*(x26r-x21r)-0.5*incx*(x26r-x21r);
y3=y4=0.6*mean;
y3r=y4r=y3-px/2;
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y9r=y14r=0;
y10r=y13r=y3l+0.5*incy*(y11r-y3r);
y21r=y28r+0.5*(y23r-y28r)-0.5*incy*(y23r-y28r);
y26r=y28r+0.4*(y23r-y28r)-0.6*incy*(y23r-y28r);
y22r=y25r=y28r+0.5*(y23r-y28r)+0.5*incy*(y23r-y28r);
y29=y30=y31=y28r+0.5*(y23r-y28r);
y29l=y30l=y29-px/2;

% branches %
x5l=x5r+taper*cont*px;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,-90);
pos12(whatever,right,-90);
pos13(whatever,up,180);
pos14(whatever,up,180);
pos21(whatever,up,0);
pos22(whatever,up,0);
pos23(whatever,right,90);
pos24(whatever,right,90);
pos25(whatever,up,180);
pos26(whatever,up,180);
pos27(whatever,right,-90);
pos28(whatever,right,-90);
pos29(whatever,right,90);
pos30(whatever,right,90);
pos31(whatever,up,180);

% draw %
penstroke super_qr(4e,5e)--super_ql(6e,7e)--super_qr(8e,1e)--super_ql(2e,3e)--cycle;
penstroke z6e--z13e&super_ql(13e,12e);
penstroke super_qr(28e,21e)--super_ql(22e,23e)--super_qr(24e,25e);
numeric t,u;
path a,b;
t:=ypart(((-infinity,y3l+1.2apert*(y11r-y3l))--(infinity,y3l+1.2apert*(y11r-y3l))) intersectiontimes (super_ql(10l,11l)--z12l));
z99=point t of (super_ql(10l,11l)--z12l);	
z98=z99+whatever*(dir(angle(direction t of (super_ql(10l,11l)--z12l))+90));
x98=w;
a:=subpath (t,infinity) of (super_ql(10l,11l)--z12l);
u:=ypart((z99--z98) intersectiontimes (super_ql(10r,11r)--z12r));
b:=subpath (infinity,u) of (super_ql(10r,11r)--z12r);
fill a--z12l--b--cycle;
t:=ypart(((-infinity,0.5mean-apert*0.5mean)--(infinity,0.5mean-apert*0.5mean)) intersectiontimes (super_ql(26l,27l)--z28l));
z100=point t of (super_ql(26l,27l)--z28l);
z101=z100+whatever*(dir(angle(direction t of (super_ql(26l,27l)--z28l))+90));
x101=0;
a:=subpath (t,infinity) of (super_ql(26l,27l)--z28l);
u:=ypart((z100--z101) intersectiontimes (super_ql(26r,27r)--z28r));
b:=subpath (infinity,u) of (super_ql(26r,27r)--z28r);
fill a--z28r--b--cycle;
z102=whatever[z30r,z29r]=whatever[z24l,z25l];
if (incy=0):
 fill z29l--z29r--z102--z31l--z30l--cycle;
else:
 penstroke z25e--z31e;
 penstroke z29e--z30e;fi

penlabels(range 1 thru 150);

endchar;