% box dimension definition %
beginadjchar("q",4.4,mean#,des#);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.2u#,space+0.4u#);

% overshoot %
top y3=mean+o;
top y4=mean+o;
y3r=y4r=mean-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x9=x10=w-x1;
x5r=x6r=x9r=x10r=x9-(cont*px/2);
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
y9=-d;
y10=mean;

% branches %
x5l=x6l=x5r+taper;

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos8(whatever,-90);
penpos9(whatever,0);
penpos10(whatever,0);

% draw %
penstroke super_qr(4e,5e)---super_ql(6e,7e)---super_qr(8e,1e)---super_ql(2e,3e)---cycle;
penstroke z9e---z10e;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.35h);

penlabels(range 1 thru 150);

endchar;