%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Travellion v1.6 – a MetaFlop-Font                       %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado     %%%
%%% www.metaflop.com                                        %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("p",4.4,mean#,des#);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(0.7u#,0.5u#);

%%% overshoot %%%
y4r=y3r=h-px +o; % overshoot up
y8r=y7r=px -o; % overshoot down
top y3=h +o; % overshoot up
top y4=h +o; % overshoot up
bot y8=0 -o; % overshoot down
bot y7=0 -o; % overshoot down

%%% construction %%%
x11=x22=leftstemloc;
x11r=x22r=x11+(cont*px/2);
x8r=if (incx>.5): .5w else: x1r+incx*(x6r-x1r) fi;
x7r=if (incx>.5): .5w else: x6r-incx*(x6r-x1r) fi;
x8r=x3r; x7r=x4r;
y11=-d;
y22=h;
x1=leftstemloc;
x6=w-x1;
x6=x5;
y2r=y5r;
y6r=y1r;
x2=x1;
x1r=x2r=x1+(cont*px/2);
x6r=x5r=x6-(cont*px/2);
y1r=if (incy>.5): .5h else: y8r-incy*(y8r-y3r) fi;
y2r=if (incy>.5): .5h else: y3r+incy*(y8r-y3r) fi;
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos11(whatever,0);
penpos22(whatever,0);
penpos1(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos2(whatever,if (superness=.5): ((ang-90)/2) else: 0 fi);
penpos3(whatever,if (superness=.5): -(180-ang)/2 else: -90 fi);
penpos4(whatever,if (superness=.5): (180-ang)/2 else: -90 fi);
penpos5(whatever,if (superness=.5): -(ang-90)/2 else: 180 fi);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);
if (cont>1.5): x2l:=x2r-px; x1l:=x2l; fi

%%% draw %%%
penstroke super_qr(4e,5e)---super_ql(6e,7e)---super_qr(8e,1e)---super_ql(2e,3e)---cycle;
penstroke z11e..z22e;

% penlabels(range 1 thru 30);

endchar;