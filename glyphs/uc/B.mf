% box dimension definition %
beginadjchar("B",4.5,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.5u#,space+0.25u#);

% construction %
x1=x2=x3=x8=x9=x14=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x11=x12=w-x1;
x5=x6=w-x1-(px/2);
x11r=x12r=x11-(cont*px/2);
x5r=x6r=x6-(cont*px/2);
x4r=x7r=x10r=x13r=x2r+0.5*(x11r-x2r)+0.5*incx*(x11r-x2r);
y1=0;
y2=h;
bot y13=0;
bot y14=0;
y13l=y14l=y13-px/2;
top y3=h;
top y4=h;
y3r=y4r=h-px;
y7=y8=y9=y10=1.05*bar*h;
y7r=y8r=y7+.5px;
y9l=y10l=y9+.5px;
y5r=y7r+.5*(y4r-y7r)+incy*.5*(y4r-y7r);
y6r=y7r+.5*(y4r-y7r)-incy*.5*(y4r-y7r);
y11r=y13r+0.5*(y7l-y13r)+0.5*incy*(y7l-y13r);
y12r=y13r+0.5*(y7l-y13r)-0.5*incy*(y7l-y13r);

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos9(whatever,right,90);
pos10(whatever,right,90);
pos11(whatever,up,180);
pos12(whatever,up,180);
pos13(whatever,right,-90);
pos14(whatever,right,-90);

% draw %
penstroke z1e--z2e;
penstroke z3e--super_qr(4e,5e)--super_ql(6e,7e)--z8e;
penstroke z9e--super_qr(10e,11e)--super_ql(12e,13e)--z14e;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.275h);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;