% box dimension definition %
beginadjchar("B",4.5,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.5u#,space+0.25u#);

% construction %
x1=x2=x3=x8=x9=x14=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=x11=x12=w-x1;
x5r=x6r=x11r=x12r=x6-(cont*px/2);
x4r=x7r=x10r=x13r=x2r+0.5*(x6r-x2r)+0.5*incx*(x6r-x2r);
y1=0;
y2=h;
bot y13=0;
bot y14=0;
y13l=y14l=y13-px/2;
top y3=h;
top y4=h;
y3r=y4r=h-px;
y7=y8=y9=y10=1.05*bar*h;
y7r=y8r=y7+.5px;
y9l=y10l=y9+.5px;
y5r=y7r+.5*(y4r-y7r)+incy*.5*(y4r-y7r);
y6r=y7r+.5*(y4r-y7r)-incy*.5*(y4r-y7r);
y11r=y13r+0.5*(y7l-y13r)+0.5*incy*(y7l-y13r);
y12r=y13r+0.5*(y7l-y13r)-0.5*incy*(y7l-y13r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos8(whatever,-90);
penpos9(whatever,90);
penpos10(whatever,90);
penpos11(whatever,180);
penpos12(whatever,180);
penpos13(whatever,-90);
penpos14(whatever,-90);

% draw %
penstroke z1e---z2e;
penstroke z3e---super_qr(4e,5e)---super_ql(6e,7e)---z8e;
penstroke z9e---super_qr(10e,11e)---super_ql(12e,13e)---z14e;

penlabels(range 1 thru 150);

endchar;