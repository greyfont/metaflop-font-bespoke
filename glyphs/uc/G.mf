% box dimension definition %
beginadjchar("G",6.5,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.25u#,space+0.35u#);

% overshoot %
top y3=h+o;
top y4=h+o;
y3r=y4r=h-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=x9=x10=x11=w-x1;
x5r=x6r=x9r=x10r=x6-(cont*px/2);
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
x12=.5w;
y9=0;
y10=y11r=y12r=y12+px/2;
y12=y11=bar*h;
y1r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos8(whatever,-90);
penpos9(whatever,0);
penpos10(whatever,0);
penpos11(whatever,90);
penpos12(whatever,90);

% draw %
penstroke z9e--z10e;
penstroke z11e--z12e;

penstroke super_ql(6e,7e)--z7e--super_qr(8e,1e)--z1e--z2e&super_ql(2e,3e);
z99=((0,0.5h+0.9*appert*0.5h)--(infinity,0.5h+0.9*appert*0.5h)) intersectionpoint (z3l--super_qr(4l,5l)--z6l);
path x; numeric t,u; (t,u)=((0,0.5h+0.9*appert*0.5h)--(infinity,0.5h+0.9*appert*0.5h)) intersectiontimes (z3l--super_qr(4l,5l)--z6l);
z98=z99+whatever*(dir(angle(direction u of (z3l--super_qr(4l,5l)--z6l))+90));
x98=0;
path b; numeric t,u; (t,u)=((z99--z98) intersectiontimes (z3r--super_qr(4r,5r)--z6r));
b = subpath (0,u) of (z3r--super_qr(4r,5r)--z6r);
path a; numeric t,u; (t,u)=(((0,0.5h+0.9*appert*0.5h)--(infinity,0.5h+0.9*appert*0.5h)) intersectiontimes (z3l--super_qr(4l,5l)--z6l));
a = subpath (u,0) of (z3l--super_qr(4l,5l)--z6l);
fill a--z3l--z3r--b--cycle;

% composite glyphs %
charanchortops_[charcode]=(.5w,1.25h+.5px);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;
