% box dimension definition %
beginadjchar("R",4.5,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.5u#,space+0.05u#);

% construction %
x1=x2=x3=x8=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=w-x1;
x5r=x6r=x6-(cont*px/2);
x4r=x7r=x2r+0.45*(x6r-x2r)+0.45*incx*(x6r-x2r);
x9r=w-x1l;
y1=y9=0;
y2=h;
top y3=h;
y3=y4;
y3r=y4r=h-px;
y7=y8=0.9bar*h;
y7r=y8r=y7+.5px;
y5r=y7r+.5*(y4r-y7r)+incy*.5*(y4r-y7r);
y6r=y7r+.5*(y4r-y7r)-incy*.5*(y4r-y7r);

% diagonal angle
alphar1=diag_ratio(1,cont*px,h,x6r-x2r);

% penpositions %
penpos1(whatever,0);
penpos2(whatever,0);
penpos8(whatever,-90);
penpos3(whatever,90);
penpos4(whatever,90);
penpos5(whatever,180);
penpos6(whatever,180);
penpos7(whatever,-90);
penpos9(alphar1*1.1*cont*px,0);

% draw %
penstroke z1e--z2e;
penstroke z3e--super_qr(4e,5e)--super_ql(6e,7e)--z8e;
path ci; ci = super_ql(6l,7l)--z8l;
path dl; dl = z9l--z2l;
path dr; dr = z9r--z2r;
path rl; numeric t,u; (t,u)=(ci intersectiontimes dl);
rl = subpath (u,0) of dl;
path rr; numeric t,u; (t,u)=(ci intersectiontimes dr);
rr = subpath (0,u) of dr;
fill rr--rl--cycle;

% composite glyphs %
charanchortops_[charcode]=(.4w,1.25h);
charanchorbots_[charcode]=(.5w,0);

penlabels(range 1 thru 150);

endchar;