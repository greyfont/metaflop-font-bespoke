% box dimension definition %
beginadjchar(215,8,cap#,0);

% sidebearings %
numeric l,r;
normal_adjust_fit(space+0.25u#,space+0.15u#);

% overshoot %
top y3=h+o;
top y4=h+o;
y3r=y4r=h-px+o;
bot y7=0-o;
bot y8=0-o;
y7r=y8r=px-o;

% construction %
x1=x2=leftstemloc;
x1r=x2r=x1+(cont*px/2);
x5=x6=x9=x10=x11=x13=x15=.65w;
x5r=x6r=x9l=x10l=x6-(cont*px/2);
x4r=x7r=x1r+0.5*(x6r-x1r)+0.5*incx*(x6r-x1r);
x3r=x8r=x1r+0.5*(x6r-x1r)-0.5*incx*(x6r-x1r);
y1r=y6r=y8r+0.5*(y3r-y8r)-0.5*incy*(y3r-y8r);
y2r=y5r=y8r+0.5*(y3r-y8r)+0.5*incy*(y3r-y8r);
x12=x16=w-x1l;
x14=w-x1;
y9=0;
y10=h;
top y11=h;
top y12=h;
y13=y14=bar*h;
bot y15=0;
bot y16=0;
y11l=y12l=y11-px/2;
y13l=y14l=y13-px/2;
y15l=y16l=y15-px/2;

% penpositions %
pos1(whatever,up,0);
pos2(whatever,up,0);
pos3(whatever,right,90);
pos4(whatever,right,90);
pos5(whatever,up,180);
pos6(whatever,up,180);
pos7(whatever,right,-90);
pos8(whatever,right,-90);
pos9(whatever,up,0);
pos10(whatever,up,0);
pos11(whatever,right,90);
pos12(whatever,right,90);
pos13(whatever,right,90);
pos14(whatever,right,90);
pos15(whatever,right,90);
pos16(whatever,right,90);

% draw %
penstroke super_qr(4e,5e)--super_ql(6e,7e)--super_qr(8e,1e)--super_ql(2e,3e)--cycle;
penstroke z9e--z10e;
penstroke z11e--z12e;
penstroke z13e--z14e;
penstroke z15e--z16e;

penlabels(range 1 thru 150);

endchar;