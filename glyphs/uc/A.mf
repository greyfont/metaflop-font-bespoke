%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                         %
% Bespoke v1.7                                                            %
% a Metaflop-font © 2012 by Simon Egli, Marco Müller                      %
% thanks to Teofilo Furtado                                               %
% www.metaflop.com                                                        %
%                                                                         %
% This program is free software: you can redistribute it and/or modify    %
% it under the terms of the GNU General Public License as published by    %
% the Free Software Foundation, either version 3 of the License, or       %
% (at your option) any later version.                                     %
%                                                                         %
% This program is distributed in the hope that it will be useful,         %
% but WITHOUT ANY WARRANTY; without even the implied warranty of          % 
% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            % 
% GNU General Public License for more details.                            %
%                                                                         %
% You should have received a copy of the GNU General Public License       %
% along with this program. If not, see <http://www.gnu.org/licenses/>.    %
%                                                                         %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% box dimension definition %
beginadjchar("A",5.5,cap#,0);		

% sidebearings %
if (sidebearing=1):
numeric l,r;
normal_adjust_fit(space-0.4u#,space-0.4u#);
fi

% construction %
x3l=leftstemloc-cont*px/2;
x2r=w-x3l;
(x1r+x4l)/2=.5w;
x1r=x4l+corner;
y1=y4=h;
y2=y3=0;

% penpositions %
alphaa:=diag_ratio(1,cont*px,h,x2r-x1r);
penpos1(alphaa*cont*px,0);
penpos2(alphaa*cont*px,0);
penpos3(alphaa*px,0);
penpos4(alphaa*px,0);
penpos5(px,90);
penpos6(px,90);

% inner top corner %
z0=whatever[z3r,z4r]=whatever[z1l,z2l];
y5=y6=bar*y0;
z5=whatever[z3,z4]; z6=whatever[z1,z2];

% draw %
fill z3l---z4l---z1r---z2r---z2l---z0---z3r---cycle;
penstroke z5e---z6e;

%penlabels(range 0 thru 30);

endchar;
