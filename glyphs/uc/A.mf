%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%                                                                         %%%
%%% Bespoke v1.6 – a Metaflop-Font                                          %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado                     %%%
%%% www.metaflop.com                                                        %%%
%%%                                                                         %%%
%%% This program is free software: you can redistribute it and/or modify    %%%
%%% it under the terms of the GNU General Public License as published by    %%%
%%% the Free Software Foundation, either version 3 of the License, or       %%%
%%% (at your option) any later version.                                     %%%
%%%                                                                         %%%
%%% This program is distributed in the hope that it will be useful,         %%%
%%% but WITHOUT ANY WARRANTY; without even the implied warranty of          %%% 
%%% MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the            %%% 
%%% GNU General Public License for more details.                            %%%
%%%                                                                         %%%
%%% You should have received a copy of the GNU General Public License       %%%
%%% along with this program. If not, see <http://www.gnu.org/licenses/>.    %%%
%%%                                                                         %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("A",5.5,cap#,0);		

%%% sidebearings %%%
if (sidebearing=1):
numeric l,r;
normal_adjust_fit(space-0.3u#,space-0.3u#);
fi

%%% construction %%%
x3l=leftstemloc-cont*px/2;		% position of the bottom left x cordinate, position of the leftstemloc 
x2r=w-x3l;						% position of the bottom right x cordinate
(x4l+x1r)/2=.5w;				% center of the tip 
x1r-x4l=corner;					% top corner
y1=y4=h;						% capital height
y2=y3=0;						% baseline
alpha:=diag_ratio(1,cont*px,h,x2r-x1r);	% diagonal definition from computer modern, see volume E, (to have optical correction turn 1 into new variable)

%%% penpositions %%%
penpos1(alpha*cont*px,0);
penpos2(alpha*cont*px,0);
penpos3(alpha*px,0);
penpos4(alpha*px,0);
penpos5(px,90);					% correct to the proper angle
penpos6(px,90);					% correct to the proper angle

%%% inner top corner %%%
z0=whatever[z3r,z4r]=whatever[z1l,z2l];	% get inner corner point
y5=y6=bar*y0;							% relation between barheight and inner corner
z5=whatever[z3,z4]; z6=whatever[z1,z2];	% correct to the proper angle

%%% draw %%%
fill z3l--z4l--z1r--z2r--z2l--z0--z3r--cycle;	% Here we use fill instead of penstroke
penstroke z5e..z6e;				% here we were lazy so we used penstroke

%penlabels(range 0 thru 30);	% labeling of pen positions including l and r, on greyfont outputs

endchar;
