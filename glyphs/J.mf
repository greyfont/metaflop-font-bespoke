%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Travellion v1.6 – a MetaFlop-Font                       %%%
%%% © 2012 by Simon Egli, Marco Müller, Teofilo Furtado     %%%
%%% www.metaflop.com                                        %%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% box dimension definition %%%
beginadjchar("J",corr*4.5,cap#,0);

%%% sidebearings %%%
numeric l,r;
normal_adjust_fit(0u#,0.9u#);

%%% overshoot %%%
y8r=y7r=px -o; % overshoot down
bot y8=0 -o; % overshoot down
bot y7=0 -o; % overshoot down

%%% construction %%%
x7r=if (incx>.5): w-incx*(w) else: x6r-incx*((x6r-(w-x6r))) fi;
x8r=if (incx>.5): x7r else: (w-x6r)+incx*(x6r-(w-x6r)) fi;
y1r=px+incy*.7((h-px)-px);     % .7 set start of lower curve
if (y1r>2/5h): y1r:=2/5h; fi
x1=if (incx>.5): x7r-((w-(leftstemloc-.5(px*cont)+.5px))-x7r) else: leftstemloc-.5(px*cont)+.5px fi;
y2=y3=h;
x2=x1=x2l+.5px;
x3=x6=x3l-.5(cont*px);
x6=w-leftstemloc;
y6r=y1r;
x1r=x1+(px/2);
x6r=x6-(cont*px/2);
x4=.5[x8,x7]; x5=x3;
top y4=h;
y4=y5;
ang:=angle(z6r-z7r);

%%% penpositions %%%
penpos1(whatever,if (superness=.5): -(ang-90)/2 else: 0 fi);
penpos2(whatever,0);
penpos3(whatever,180);
penpos6(whatever,if (superness=.5): (ang-90)/2 else: 180 fi);
penpos7(whatever,if (superness=.5): -(180-ang)/2 else: 90 fi);
penpos8(whatever,if (superness=.5): (180-ang)/2 else: 90 fi);
penpos4(px,90);
penpos5(px,90);

%%% draw %%%
penstroke z3e---super_ql(6e,7e);
%penstroke z4e---z5e;
if (incx > .5):
	z100=((((leftstemloc-.5(cont*px)),-infinity)--((leftstemloc-.5(cont*px)),h+infinity)) intersectionpoint (super_ql(6l,7l)---super_qr(8l,1l)));
	numeric t,u; (t,u)=((((leftstemloc-.5(cont*px)),-infinity)--((leftstemloc-.5(cont*px)),h+infinity)) intersectiontimes (super_ql(6l,7l)---z7l---super_qr(8l,1l)));
if (x8l<leftstemloc-.5(cont*px)): x101=x100; else: z101=z100+whatever*(dir(angle(direction u of (super_ql(6l,7l)---z7l---super_qr(8l,1l)))-90));fi
	y101=h;
	path r; numeric t,u; (t,u)=((z100--z101) intersectiontimes (super_qr(8r,1r)---z1l));
	r = subpath (0,u) of (super_qr(8r,1r)---z1l);
	path ss; numeric t,u; (t,u)=((((leftstemloc-.5(cont*px)),-infinity)--((leftstemloc-.5(cont*px)),h+infinity)) intersectiontimes (super_ql(6l,7l)---z7l---super_qr(8l,1l)));
	ss = subpath (u,0) of (super_ql(6l,7l)---z7l---super_qr(8l,1l));
	fill ss---super_ql(6r,7r)---r---cycle;
else:
	z100=((-infinity,(h/2-appert*(h/2-y7)))--(infinity,(h/2-appert*(h/2-y7)))) intersectionpoint (super_qr(8l,1l)---z2l);
	path x; numeric t,u; (t,u)=((((-infinity,(h/2-appert*(h/2-y7)))--(infinity,(h/2-appert*(h/2-y7))))) intersectiontimes (super_qr(8l,1l)---z2l));
	z101=z100+whatever*(dir(angle(direction u of (super_qr(8l,1l)---z2l))+90));
	x101=w;
	path r; numeric t,u; (t,u)=((z100--z101) intersectiontimes (super_qr(8r,1r)---z2r));
	r = subpath (u,0) of (super_qr(8r,1r)---z2r);
	path ss; numeric t,u; (t,u)=(((0,(h/2-appert*(h/2-y7)))--(infinity,(h/2-appert*(h/2-y7)))) intersectiontimes (super_qr(8l,1l)---z2l));
	ss = subpath (0,u) of (super_qr(8l,1l)---z2l);
	fill ss---r---z7r---z7l---cycle;
	fi

%penlabels(1,2,3,4,5,6,7,8);
%labels (range 50 thru 200);

endchar;